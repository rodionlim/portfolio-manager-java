plugins {
    id 'java-library'
    id 'application'
    id 'com.diffplug.spotless' version '6.25.0'
}

group = 'org.rodion.pfm'
version = '1.0-SNAPSHOT'

if (!JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)) {
    throw new GradleException("Java 17 or later is required to build Portfolio Manager.\n" +
            "  Detected version ${JavaVersion.current()}")
}

repositories {
    mavenCentral()
}

apply from: 'gradle/versions.gradle'

ext {
    javaMainClass = "org.rodion.pfm.PortfolioManager"
}

jar {
    archiveBaseName = 'portfolio-manager-java'
    manifest {
        attributes(
                'Specification-Title': archiveBaseName,
                'Specification-Version': project.version,
                'Implementation-Title': archiveBaseName,
                'Main-Class': javaMainClass,
        )
    }
}

spotless {
    java {
        target fileTree('.') {
            include '**/*.java'
            exclude '**/build/**', '**/build-*/**'
        }
        toggleOffOn()
        googleJavaFormat(googleJavaFormat)
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

application {
    mainClass = javaMainClass
}

dependencies {
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"

    implementation "com.google.dagger:dagger:${daggerVersion}"
    implementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    implementation "org.apache.logging.log4j:log4j-slf4j2-impl:${log4jVersion}"
    implementation "info.picocli:picocli:${picocliVersion}"
    implementation "io.netty:netty-all:${nettyVersion}"

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
}

test {
    useJUnitPlatform()
}